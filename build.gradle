buildscript {
    apply from: 'versions.gradle'

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://jitpack.io" }
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    }
}

allprojects {
    group 'io.github.kmpp'
    version '1.2.31'

    repositories {
        mavenCentral()
    }
}

apply plugin: "kotlin"

subprojects {

    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    afterEvaluate {
        task sourceJar(type: Jar, dependsOn: classes) {
            classifier 'sources'
            from sourceSets.main.kotlin
            duplicatesStrategy = "exclude"
            def platformSrc = sourceSets.main.kotlin
            def commonSrc = project(':kmpp-logging-common').sourceSets.main.kotlin
            from (platformSrc + commonSrc)
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
    }

    def varintName = "${project.name}"
    tasks.withType(Jar) {
        archivesBaseName = varintName
    }

    // Create the pom configuration:
    def pomConfig = {
        name "kmpp-logging"
        description "kmpp-logging $version - Multiplatform logging for Kotlin"
        url "https://github.com/kmpp/kmpp-logging"

        scm {
            url "https://github.com/kmpp/kmpp-logging/tree/master"
            connection "scm:git:git://github.com/kmpp/kmpp-logging.git"
            developerConnection "scm:git:ssh://github.com:kmpp/kmpp-logging.git"
        }
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id "abissell"
                name "Andrew Bissell"
                email "abissell@gmail.com"
            }
        }
    }

    publishing {
        publications {
            mavenProject(MavenPublication) {
                from components.java
                groupId project.group
                artifactId varintName
                version project.version

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', "kmpp-logging $version - Multiplatform logging for Kotlin")
                    root.children().last() + pomConfig
                }

                artifact sourceJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }

    bintray {
        user = System.getProperty('bintray.user')
        key = System.getProperty('bintray.key')
        publications = ['mavenProject']
        pkg {
            repo = 'kmpp-logging'
            name = 'kmpp-logging'
            userOrg = 'kmpp'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/kmpp/kmpp-logging'
            websiteUrl = 'https://github.com/kmpp/kmpp-logging'
            issueTrackerUrl = 'https://github.com/kmpp/kmpp-logging/issues'

            githubRepo = 'kmpp/kmpp-logging'
            version {
                name = project.version
                desc = "kmpp-logging - Multiplatform logging for Kotlin"
                released = new Date()
            }
        }
    }
}
